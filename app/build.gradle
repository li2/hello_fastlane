apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "me.li2.android.hello_fastlane"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "${applicationId}-v${versionName}(${versionCode})")
    }

    buildTypes {
        debug {
            debuggable = true
            signingConfig signingConfigs.debug
        }
        release {
            debuggable = false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "environment"

    productFlavors {
        development {
            applicationIdSuffix ".dev"
            dimension "environment"
        }

        uat {
            applicationIdSuffix ".uat"
            dimension "environment"
        }

        production {
            dimension "environment"
        }
    }

    signingConfigs {
        config
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

// set the keystore if we set the path
if (KEYSTORE != "") {
    // initialize the signing
    android.signingConfigs.config.storeFile = file('../keystore/' + KEYSTORE)
    android.signingConfigs.config.storePassword = STORE_PASSWORD
    android.signingConfigs.config.keyAlias = KEY_ALIAS
    android.signingConfigs.config.keyPassword = KEY_PASSWORD
    // set this signing for both build types
    android.buildTypes.release.signingConfig = android.signingConfigs.config
    android.buildTypes.debug.signingConfig = android.signingConfigs.config
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}